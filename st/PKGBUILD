# Maintainer: sndb <sndb@sndb.xyz>

pkgname=sndb-st-git
_pkgname=st
pkgver=0.8.5.r4.e823e23
pkgrel=1
pkgdesc='A simple terminal implementation for X'
arch=('x86_64')
url='https://st.suckless.org/'
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://git.suckless.org/$_pkgname"
        "config.h"
        "anysize.diff::https://st.suckless.org/patches/anysize/st-anysize-0.8.4.diff"
        "copyurl.diff::https://st.suckless.org/patches/copyurl/st-copyurl-20220217-0.8.5.diff")
md5sums=('SKIP'
         'SKIP'
         'db025b249ee0b63ea6831f09a7d26655'
         '7ce7b62c07456b3a06e676f539aad695')

pkgver() {
	cd "$srcdir/$_pkgname"
	printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
	cd "$srcdir/$_pkgname"
	cp -v "$srcdir/config.h" .
	patch --forward -p1 < "$srcdir/anysize.diff"
	patch --forward -p1 < "$srcdir/copyurl.diff"
}

build() {
	cd "$srcdir/$_pkgname"
	make clean
}

package() {
	cd "$srcdir/$_pkgname"
	make DESTDIR="$pkgdir" PREFIX=/usr install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
