# Maintainer: sndb <sndb@sndb.xyz>

pkgname=sndb-dwm-git
_pkgname=dwm
pkgver=6.3.r0.d39e2f3
pkgrel=1
pkgdesc='A dynamic window manager for X'
arch=('x86_64')
url='https://dwm.suckless.org/'
license=('MIT')
depends=('libx11' 'libxinerama' 'libxft')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$pkgname::git+https://git.suckless.org/dwm"
        "etc::git+https://github.com/sndb/etc.git"
        "https://dwm.suckless.org/patches/removeborder/dwm-removeborder-20200520-f09418b.diff")
md5sums=('SKIP'
         'SKIP'
         '38fa4f557d34a53211a50431808ba51a')

pkgver() {
	cd "$srcdir/${pkgname}"
	printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
	cd "$srcdir/${pkgname}"
	cp -v "$srcdir/etc/dwm/config.h" .
	patch --forward -p1 < "$srcdir/dwm-removeborder-20200520-f09418b.diff"
}

build() {
	cd "$srcdir/${pkgname}"
	make clean
}

package() {
	cd "$srcdir/${pkgname}"
	make DESTDIR="$pkgdir/" PREFIX=/usr install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}
