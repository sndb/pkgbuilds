# Maintainer: sndb <sndb@sndb.xyz>

pkgname=sndb-dwm-git
_pkgname=dwm
pkgver=6.3.r2.bece862
pkgrel=1
pkgdesc='A dynamic window manager for X'
arch=('x86_64')
url='https://dwm.suckless.org/'
license=('MIT')
depends=('libx11' 'libxinerama' 'libxft')
makedepends=('git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+https://git.suckless.org/$_pkgname"
        "config.h"
        "scratchpad.diff::https://dwm.suckless.org/patches/dynamicscratchpads/dwm-scratchpad-20200727-bb2e7222baeec7776930354d0e9f210cc2aaad5f.diff")
md5sums=('SKIP'
         'SKIP'
         '35c20af7c1ee777f2de65a852207156c')

pkgver() {
	cd "$srcdir/$_pkgname"
	printf "%s" "$(git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
	cd "$srcdir/$_pkgname"
	cp -v "$srcdir/config.h" .
	patch --verbose --forward -p1 < "$srcdir/scratchpad.diff"
}

build() {
	cd "$srcdir/$_pkgname"
	make clean
}

package() {
	cd "$srcdir/$_pkgname"
	make DESTDIR="$pkgdir" PREFIX=/usr install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
